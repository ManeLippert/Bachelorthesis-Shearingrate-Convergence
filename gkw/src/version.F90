!-----------------------------------------------------------------------
!> This module provides gkw version information from gkw_info.h
!> (created from compile-time git describe) and reports information on 
!> executable capabilities, as selected by the preprocessing options.
!> 
!> This module depends on the gkw_info.h header, which changes whenever
!> the git version hash changes, hence this module is near the top of 
!> the dependency tree to enable faster compilations.
!-----------------------------------------------------------------------

module version

  implicit none
  
  private
  
  ! To limit depdencies, should not be used other than in init  
  public :: gkw_info, output_header
  
  ! provide "character (len=8) :: GKW_REV = <git version>",
  ! generated by git describe, called via the makefile
#ifdef NOVERSION
  character (len=32),  parameter, public :: GKW_REV = 'NOVERSION'
  character (len=32),  parameter, public :: GKW_EXE = 'gkw.x'
  character (len=128), parameter, public :: GKW_FC =  'fortran_compiler'
  character (len=128), parameter, public :: MPIRUNCMD =  'none'
#else
  include 'gkw_info.h'
#endif

contains

  !-----------------------------------------------------------------------------
  !> Report the basic code capabilities then abort. This routine is called when
  !> no input file is present.
  !-----------------------------------------------------------------------------
  subroutine gkw_info()
    use global,       only : compiled_with_slepc, compiled_with_hdf5
    use global,       only : compiled_with_openmp, openmp_date
    use global,       only : int2char, compiled_with_umfpack
    use global,       only : have_double_precision, rp
    use fft,          only : fftlib, working_fft_library
    use mpiinterface, only : mpifinalize, mpistandard, root_processor
#ifdef HAVE_HDF5
    use io_hdf5, only : get_libversion
#endif
    character (len=6) :: real_precision
    character (len=12) :: have_openmp
    character (len=1) :: have_slepc
    character (len=1) :: usable_eiv, usable_nonspec, usable_imp, usable_nonlin
    character (len=15) :: have_hdf5

    if (have_double_precision) then
      real_precision = 'double'
    else
      real_precision = 'single'
    end if

    if (compiled_with_slepc) then
      have_slepc = 'T'
      usable_eiv = 'T'
    else
      have_slepc = 'F'
      usable_eiv = 'F'
    end if

    if (compiled_with_hdf5) then
#ifdef HAVE_HDF5
      have_hdf5 = 'T - '//get_libversion()
#endif
    else
      have_hdf5 = 'F'
    end if
    
    ! Set the have_openmp string
    if (compiled_with_openmp) then
      have_openmp = 'T - '//trim(int2char(openmp_date,6))
    else
      have_openmp = 'F'
    end if

    ! Set the have_umfpack character
    if (compiled_with_umfpack > 0) then
      usable_nonspec = 'T'
    else
      usable_nonspec = 'F'
    end if

    if (compiled_with_umfpack > 0) then
      usable_imp = 'F'
    else
      usable_imp = 'F'
    end if

    if (working_fft_library) then
      usable_nonlin = 'T'
    else
      usable_nonlin = 'F'
    end if

    if (root_processor) then
      write (*,*) 'No input file input.dat found; aborting.'
      write (*,*)
      write (*,*) 'GKW requires an input file for normal operation'
      write (*,*)
      write (*,*) 'REVISION       : ',trim(GKW_REV)
      write (*,*) 'REAL_PRECISION : ',trim(real_precision),',',rp
      write (*,*) 'FFTLIB         : ',trim(fftlib)
      write (*,*) 'MPI            : ',trim(mpistandard)
      write (*,*) 'MPIRUNCMD      : ',trim(MPIRUNCMD)
      write (*,*) 'OPENMP         : ',trim(have_openmp)
      write (*,*)

      write (*,*) 'Libraries that were linked:'
      write (*,*) '  UMFPACK        : ', usable_nonspec
      write (*,*) '  UMFPACK bits   : ', compiled_with_umfpack
      write (*,*) '  SLEPC/PETSC    : ', have_slepc
      write (*,*) '  HDF5           : ', have_hdf5
#ifdef blas
      write (*,*) '  BLAS           : T'
#else
      write (*,*) '  BLAS           : F'
#endif
#ifdef HAVE_MKL
      write (*,*) '  Intel MKL      : T'
#else
      write (*,*) '  Intel MKL      : F'
#endif
#ifdef HAVE_LIBRSB
      write (*,*) '  librsb         : T'
#else
      write (*,*) '  librsb         : F'
#endif
      write (*,*)

      write (*,*) 'Which parts are usable:'
      write (*,*) '  Spectral methods     : ','T'
      write (*,*) '  Explicit integration : ','T'
      write (*,*) '  Nonlinearity         : ', usable_nonlin
      write (*,*) '  Implicit integration : ', usable_imp
      write (*,*) '  Nonspectral & global : ', usable_nonspec
      write (*,*) '  Eigenvalue solver    : ', usable_eiv
      write (*,*)
    end if

    call mpifinalize()
    stop 0

  end subroutine gkw_info

  !-----------------------------------------------------------------------------
  !> Write the header for the input.out file and call IO wrapper routine
  !> which puts this metadata also into structured output formats.
  !-----------------------------------------------------------------------------
  subroutine output_header(lun)

    use mpiinterface, only : number_of_processors, root_processor
    use mpiinterface, only : mpibcast
    use specfun,      only : timestamp
    use io, only : write_run_parameter

    integer, intent(in) :: lun
    character(len=128) :: tstamp
    tstamp = timestamp()
    call mpibcast(tstamp, len(tstamp))
    if(root_processor) then
      ! Write the revision information at compile time.
      ! APS: put the other information in here too
      ! APS: also put it in the HDF5 file
      write(lun,'(A)') '! Output data generated by GKW version '//trim(GKW_REV)
      write(lun,'(A)') '! '//trim(tstamp)
      write(lun,'(A)') '! executable name: '//trim(GKW_EXE)
      write(lun,'(A)') '! compiled with $(FC) '
      write(lun,'(A)') '! '//trim(GKW_FC)
      write(lun,*) '!Run on ', number_of_processors, 'processor(s)'
      write(lun,*) '!If the input file generated warnings:'
      write(lun,*) '!This file is a clean version that should not generate warnings'
      write(lun,*) '!The following are the input variables as used by the code:'
    end if

    call write_run_parameter('header','gkw_version',trim(GKW_REV))
    call write_run_parameter('header','timestamp',trim(tstamp))
    call write_run_parameter('header','gkw_executable_name',trim(GKW_EXE))
    call write_run_parameter('header','compiler',trim(GKW_FC))
    call write_run_parameter('header','number_of_processors',number_of_processors)
  end subroutine output_header

end module version
